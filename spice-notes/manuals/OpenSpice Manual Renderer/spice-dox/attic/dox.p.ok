compile_mode :pop11 +strict;


;;; -- Syntactic Properties -----------------------------------
;;;
;;; The SynProps record defines the syntactic properties
;;; associated with tokens.
;;;     strength            block-level flag    "strong", "weak" or "daft"
;;;     style               fixity              "x", "F", "Fx", "xF", "xFx", "FxendF" or "endF"
;;;     opening bracket }   pair of words that act as brackets for outfix syntax
;;;     closing bracket }
;;;     rank                precedence level
;;;     action              element name
;;;


defclass SynProps {
    synPropsStrength,
    synPropsStyle,
    synPropsOpeningBracketAux,
    synPropsClosingBracketAux,
    synPropsRank,
    synPropsAction
};

define newAnySynProps( strength, style, opener, closer, rank, action );
    lvars is_brackets = ( style == "FxendF" or style == "endF" );
    if opener then
        unless is_brackets then
            mishap( 'Invalid opener', [ ^opener ] )
        endunless
    endif;
    if closer then
        unless is_brackets then
            mishap( 'Invalid closer', [ ^closer ] )
        endunless
    endif;
    unless rank.isinteger do
        mishap( 'Invalid rank', [ ^rank ] )
    endunless;
    unless lmember( strength, [ weak strong daft ] ) do
        mishap( 'Invalid strength', [ ^strength ] )
    endunless;
    unless lmember( style, [ F x Fx xFx $ FxendF endF ] ) do
        mishap( 'Invalid style', [ ^style ] )
    endunless;
    unless action.isword or action == false do
        mishap( 'Invalid action', [ ^action ] )
    endunless;
    consSynProps( strength, style, opener, closer, rank, action )
enddefine;

define newOpSynProps( strength, style, rank, action );
    newAnySynProps(  strength, style, false, false, rank, action )
enddefine;

define newSingleSynProps( strength, style, action );
    newAnySynProps( strength, style, false, false, 0, action )
enddefine;

define newPairedSynProps( strength, opener, closer, action );
    newAnySynProps( strength, "FxendF", opener, closer, 0, action );
    newAnySynProps( strength, "endF", opener, closer, 0, action );
enddefine;

define synPropsClosingBracket( s );
    lvars b = s.synPropsClosingBracketAux;
    unless b.isword do
        mishap( 'Trying to use undefined closer', [% s %] )
    endunless;
    b
enddefine;

define synPropsOpeningBracket( s );
    lvars b = s.synPropsOpeningBracketAux;
    unless b.isword do
        mishap( 'Trying to use undefined closer', [ ^s ] )
    endunless;
    b
enddefine;


;;; -- Token --------------------------------------------------
;;;
;;; The output of the tokenizer is a Token which is effectively
;;; a pair ( item x syntactic-properties ).  The item is a word,
;;; a string, or termin.
;;;

defclass Token {
    tokenValue,
    tokenSynProps
};

define newToken( item, synprops );
    unless item.isword or item.isstring or item == termin do
        mishap( 'Invalid item for Token', [ ^item ] )
    endunless;
    unless synprops.isSynProps do
        mishap( 'Invalid SynProps for Token', [ ^synprops ] )
    endunless;
    consToken( item, synprops )
enddefine;


;;; -- Meanings -----------------------------------------------

defclass Meaning {
    meaningStrength,
    meaningArg,
    meaningAction
};

define newAnyMeaning( token, strength, arg );
    unless lmember( strength, [ weak strong daft ] ) do
        mishap( 'Invalid Meaning strength', [ ^strength ] )
    endunless;
    unless arg.islist do
        mishap( 'Invalid arg (list needed)', [ ^arg ] )
    endunless;
    lvars syn = token.tokenSynProps;
    consMeaning(
        strength,
        arg,
        syn.synPropsAction
    )
enddefine;

define newMeaning( token, arg );
    newAnyMeaning( token, token.tokenSynProps.synPropsStrength, arg )
enddefine;

define newDaftMeaning( token, arg );
    newAnyMeaning( token, "daft", arg )
enddefine;

define global lengthMeaning( m );
    lvars A = m.meaningArg;
    lvars L = A.listlength;
    lvars i;
    for i in A do
        if i.isMeaning then
            i.lengthMeaning
        else
            i.length
        endif + L -> L
    endfor;
    L
enddefine;

;;; lengthMeaning -> class_pepperLength( Meaning_key );


define formString( s, endsWithNewline );
    ;;; [ endsWithNewline ^endsWithNewline ]=>
    lconstant weaksp = newSingleSynProps( "weak", "x", "String" );
    lconstant strongsp = newSingleSynProps( "strong", "x", "String" );
    newToken( s, if endsWithNewline then strongsp else weaksp endif )
enddefine;

define formTermin();
    newToken(
        termin,
        newSingleSynProps( "weak", "endF", false )
    )
enddefine;


;;; -- Tokenization -------------------------------------------

;;;
;;; Mode is a positive integer that counts how deeply
;;; nested we are in a quasi-quoted content.  I use the phrase
;;; quasi-quoted with some trepidation, I doubt it is correct!
;;;


defclass Tokenizer {
    tokenizerSource,
    tokenizerMode       : pint,
    tokenizerDump
};

define newTokenizer( procedure r );
    consTokenizer(
        r,              ;;; The character repeater.
        0,              ;;; In the normal context.
        []              ;;; The nested modes.
    )
enddefine;

define saveTokenizerMode( t );
    conspair( t.tokenizerMode, t.tokenizerDump ) -> t.tokenizerDump
enddefine;

define restoreTokenizerMode( t );
     t.tokenizerDump.dest ->  t.tokenizerDump -> t.tokenizerMode;
enddefine;

define enterQuasiQuotingMode( t );
    t.tokenizerMode + 1 -> t.tokenizerMode;
enddefine;


constant CHAR_plain = 0;
constant CHAR_digit = 1;
constant CHAR_letter = 2;
constant CHAR_simple = 3;
constant CHAR_sign = 4;
constant CHAR_termin = 5;
constant CHAR_slash = 6;
constant CHAR_layout = 7;
constant CHAR_dollar = 8;

lconstant answer = consstring(#| repeat 256 times CHAR_plain endrepeat |#);

constant charTable = (
    lblock

        define lconstant setType( charType, chars, table );
            lvars i;
            for i from 1 to chars.datalength do
                charType -> subscrs( subscrs( i, chars ), table )
            endfor
        enddefine;

        setType( CHAR_digit, '0123456789', answer );
        setType( CHAR_simple, '(){}[];', answer );
        setType( CHAR_layout, '\n\t\s', answer );
        setType( CHAR_slash, '\\', answer );
        setType( CHAR_dollar, '$', answer );
        setType( CHAR_sign, '!@#~%^&*+-=|:<>?/', answer );
        setType( CHAR_letter, 'abcdefghijklmnopqrstuvwxyz', answer );
        setType( CHAR_letter, 'ABCDEFGHIJKLMNOPQRSTUVWXYZ', answer );
        answer
    endlblock
);

define peekCh( procedure r );
    r() ->> r()
enddefine;

vars procedure ( lookupWord );

define nextToken( procedure r, mode );

    define lconstant cantExtend( ch, chType );
        lvars newType = subscrs( ch, charTable );
        not(
            newType == chType or
            ( chType == CHAR_letter and newType == CHAR_digit )
        )
    enddefine;

    define lconstant eatString( procedure r, ch );
        consstring(#|
            ch;
            repeat
                lvars newCh = r();
                lvars nextCh = newCh == termin and termin or r.peekCh;
                quitif(
                    newCh == termin or
                    ( newCh == `\\` and nextCh /== `\\` ) or
                    ( newCh == `\n` and nextCh == `\n` )
                );
                if newCh == `\\` and nextCh == `\\` then
                    `\\`, r() -> _
                else
                    newCh
                endif;
            endrepeat;
            newCh -> r();
        |#) @formString ( newCh == `\n` )
    enddefine;

    ;;; Dispose of leading whitespace.
    repeat
        lvars ch = r();
        quitunless( ch == ` ` or ch == `\n` or ch == `\t` );
    endrepeat;

    if ch == termin then
        formTermin()
    elseif ch == `\\` then
        lvars nextCh = r();
        if `a` <= nextCh and nextCh <= `z` then
            consword(#|
                nextCh;
                repeat
                    lvars newCh = r();
                    quitunless( `a` <= newCh and newCh <= `z` );
                    newCh
                endrepeat
            |#) @lookupWord, newCh -> r()
        elseif nextCh == `\\` then
            r @eatString nextCh
        else
            consword(#| nextCh |#) @lookupWord
        endif;
    elseif mode == 0 then       ;;; starts as 0, incremented by \$ operator.
        r @eatString ch
    elseif ch == `\"` then
        consstring(#|
            ch,
            repeat
                lvars newCh = r();
                quitif( newCh == termin or newCh == ch or newCh == `\n` );
                newCh
            endrepeat,
            ch
        |#) @formString false
    else
        lvars chType = subscrs( ch, charTable );
        if chType == CHAR_simple then
            consword(#| ch |#)
        else
            consword(#|
                ch;
                repeat
                    lvars newCh = r();
                    quitif( newCh == termin or ( newCh @cantExtend chType ) );
                    newCh
                endrepeat
            |#), newCh -> r()
        endif @lookupWord
    endif
enddefine;


define readToken( tokenizer );
    nextToken( tokenizer.tokenizerSource, tokenizer.tokenizerMode )
enddefine;

;;; -- Parser -------------------------------------------------

constant widestWidth = 200;


define makeOperand( T );
    newMeaning( T, [% T.tokenValue %] )
enddefine;

define makeNiladic( T );
    newDaftMeaning( T, [] )
enddefine;

define makeMonadic( L, T );
    newDaftMeaning( T, , [ ^L ] )
enddefine;

define makeMonadicWrapped( Ls, T );
    newDaftMeaning( T, Ls )
enddefine;

define makeDyadic( L, R, T );
    newDaftMeaning( T, [ ^L ^R ] )
enddefine;


vars unwrappers = [];

vars procedure ( parseStuff, parseRepeatedly );

define parseOperand( it, tokenizer, width ) -> ( answer, token );
    lvars it_syn = it.tokenSynProps;
    lvars s = it_syn.synPropsStyle;
    lvars rank = it_syn.synPropsRank;
;;;    [`parse operand starting`, s].reportln;
    if s == "Fx" and rank <= width then
        lvars ( R, newNext ) = readToken( tokenizer ) @parseStuff (tokenizer, rank - 1);
        R @makeMonadic it, newNext
    elseif s == "FxendF" then
        lvars wantedb = it_syn.synPropsClosingBracket;
        dlocal unwrappers = [ ^wantedb ^^unwrappers ];
        saveTokenizerMode( tokenizer );
        lvars ( X, closer ) = tokenizer @parseRepeatedly (widestWidth);
        X @makeMonadicWrapped it;
        restoreTokenizerMode( tokenizer );
        lvars gotb = closer.tokenSynProps.synPropsClosingBracket;
        if gotb == wantedb then
            ;;; good; advance to the next token ...
            readToken( tokenizer )
        else
            ;;; ah. we have a random closer.
            if lmember( gotb, unwrappers ) then
                ;;; someone missed out *our* closer! Complain & deliver it.
                warning( 'missing closer', [% wantedb, "got", gotb %] );
                closer
            else
                ;;; this one is lonely. Discard it.
                warning( 'unexpected closer', [% gotb, 'nested in', unwrappers %] );
                readToken( tokenizer )
            endif
        endif
    elseif s == "x" then
        it.makeOperand, readToken( tokenizer )
    elseif s == "F" then
        it @makeNiladic,  readToken( tokenizer )
    elseif s == "$" then
        tokenizer.enterQuasiQuotingMode;
        readToken( tokenizer ) @parseOperand (tokenizer, width)
    else
        mishap( 'what sort of starter is this?', [^it] )
    endif -> ( answer, token );
    unless answer.isMeaning do
        mishap( 'ParseOperand says Meaning needed', [ ^answer ] )
    endunless;
enddefine;

define parseStuff( this, tokenizer, width ) -> ( L, next );
    (this) @parseOperand (tokenizer, width) -> ( L, next );
    repeat
        lvars next_syn = next.tokenSynProps;
        lvars s = next_syn.synPropsStyle;
        lvars rank = next_syn.synPropsRank;
        quitunless( s == "xF" or s == "xFx" ) and ( rank <= width );
        if s == "xF" then
            ;;; [`-- that is postfix`].reportln;
            L @makeMonadic next -> L,  readToken( tokenizer ) -> next
        else
            ;;; [`-- that is infix`].reportln;
            lvars ( R, newNext ) =  readToken( tokenizer ) @parseStuff (tokenizer, rank - 1);
            (L, R) @makeDyadic next -> L, newNext -> next
        endif
    endrepeat;
    unless L.isMeaning do
        mishap( 'parseStuff says Meaning needed', [ ^L ] )
    endunless;
enddefine;

define parseRepeatedly( tokenizer, width );
    lvars this = tokenizer.readToken;
    [%
        until this.tokenSynProps.synPropsStyle == "endF" do
            (this) @parseStuff (tokenizer, width) -> this
        enduntil
    %];
    this
enddefine;


;;; -- Main ---------------------------------------------------


define defWrapper( name, endName, action );
    lvars ( a, b ) = newPairedSynProps( "strong", name, endName, action );
    ( [ ^name ^a ], [ ^endName ^b ] )
enddefine;

define defWeakWrapper( name, endName, action );
    lvars ( a, b ) = newPairedSynProps( "weak", name, endName, action );
    ( [ ^name ^a ], [ ^endName ^b ] )
enddefine;

define defSimple( name, action );
    [% name, newSingleSynProps( "strong", "F", action ) %]
enddefine;

;;; define defSimpleWrapper( name, wrapper );
;;;     lvars endName = consword(#| 'end'.explode, name.explode |#);
;;;     defWrapper( name, endName, evalWrap(% wrapper %) )
;;; enddefine;

;;; define defSimpleNoParaWrapper( name, wrapper );
;;;     lvars endName = "end" <> name;
;;;     defWeakWrapper( name, endName, evalNoParaWrap(% wrapper %) )
;;; enddefine;

;;; define defOperator( name, rank, proc );
;;;     [% name, newOpSynProps( "strong", "plain", rank, evalOp(% name, proc %) ) %]
;;; enddefine;

define defInfix( name, level, action );
    [% name, newOpSynProps( "weak", "xFx", level, action ) %]
enddefine;

define defPrefix( name, action );
    [% name, newOpSynProps( "weak", "Fx", 20, action ) %]
enddefine;

define isHeader( element_name );
    if element_name == "Chapter" or  element_name == "Appendix" then
        1
    elseif element_name == "Section" then
        2
    elseif element_name == "Passage" then
        3
    elseif element_name == "Fragment" then
        4
    else
        false
    endif
enddefine;

define synPropsTable =
    newanyproperty(
        [%
            defInfix( "linkto", 20, "LinkTo" );
            defPrefix( "label", "Label" );
            defInfix( "ref", 20, "Ref" );
            ;;; defWeakWrapper( "\(", "\)", "VisibleBrackets" );
            ;;; defWeakWrapper( "\[%", "\%]", "VisibleBoxes" );
            ;;; defSimpleNoParaWrapper( "asis", "pre" );
            defWrapper( "part", "endpart", "Part" ),
            defWrapper( "indented", "endindented", "Indented" ),
            defWrapper( "title", "endtitle", "Title" ),
            defWrapper( "chapter", "endchapter", "Chapter" ),
            defWrapper( "appendix", "endappendix", "Appendix" ),
            defWrapper( "section", "endsection", "Section" ),
            defWrapper( "passage", "endpassage", "Passage" ),
            defWrapper( "fragment", "endfragment", "Fragment" ),
            defWrapper( "list", "endlist", "List" ),
            defWrapper( "syntax", "endsyntax", "Syntax" ),
            ;;; defWrapper( "issue", "endissue", "Issue" ),
            defWrapper( "row", "endrow", "Row" ),
            defWrapper( "spice", "endspice", "Spice" ),
            defWrapper( "table", "endtable", "Table" ),
            defSimple( "allsyntax", "AllSyntax" ),
            defSimple( "allissues", "AllIssues" ),
            ;;; defSimple( "n", "N" ),
            ;;; defSimple( "t", "T" ),
            defSimple( "contents", "Contents" ),
            [ $ % newSingleSynProps( "daft", "$", false ) %]
        %].dup.length, 1, false,
        false, false, "perm",
        newSingleSynProps( "weak", "x", "Word" ),
        false
    )
enddefine;

define lookupWord( w );
    lvars syn = w.synPropsTable;
    newToken( w, syn )
enddefine;


;;; -----------------------------------------------------------
